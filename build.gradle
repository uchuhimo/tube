plugins {
    id "idea"
    id "com.dorongold.task-tree" version "1.2.2"
}

idea {
    project {
        jdkName = "1.8.0_51"
        languageLevel = "1.8"
        vcs = "Git"

        ipr.withXml { provider ->
            def gradleSettings = """
  <component name="GradleSettings">
    <option name="linkedExternalProjectsSettings">
      <GradleProjectSettings>
        <option name="distributionType" value="WRAPPED" />
        <option name="externalProjectPath" value="\$PROJECT_DIR\$" />
        <option name="gradleJvm" value="#JAVA_HOME" />
        <option name="modules">
          <set>
            <option value="\$PROJECT_DIR\$" />
          </set>
        </option>
      </GradleProjectSettings>
    </option>
  </component>
"""
            provider.node.append(new XmlParser().parseText(gradleSettings))
        }
    }
}

allprojects {
    group "uchuhimo"
    version "0.1"

    repositories {
        //mavenCentral()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
        jcenter()
    }
    
    task wrapper(type: Wrapper) {
        gradleVersion = "3.2"
        distributionType = Wrapper.DistributionType.ALL
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "findbugs"
    apply plugin: "checkstyle"

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    
    tasks.withType(Test) {
        testLogging.showStandardStreams = true
    }
    
    findbugs {
        ignoreFailures = true
        //effort = "default"
        effort = "max"
        //reportLevel = "medium"
        reportLevel = "low"
        excludeFilter = file("${rootProject.projectDir}/config/findbugs/excludeFilter.xml")
    }
    
    checkstyle {
        ignoreFailures = true
        toolVersion = "7.2"
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
        configProperties.checkstyleConfigDir = config.asFile().parent
    }
    
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}
